plugins {
  id "java"
  id "org.springframework.boot" version "2.7.5"
  id "io.spring.dependency-management" version "1.1.0"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.5.0.2730"
}

group = "uk.nhs.hee.tis.trainee"
version = "0.0.2"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.3"
    mavenBom("io.awspring.cloud:spring-cloud-aws-dependencies:2.4.4")
  }
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-validation"
  implementation "org.springframework.boot:spring-boot-starter-web"
  testImplementation "org.springframework.boot:spring-boot-starter-test"

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // MapStruct
  ext.mapstructVersion = "1.5.3.Final"
  implementation "org.mapstruct:mapstruct:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  // Sentry reporting
  ext.sentryVersion = "6.14.0"
  implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
  implementation "io.sentry:sentry-logback:$sentryVersion"

  // Amazon SQS
  implementation "io.awspring.cloud:spring-cloud-starter-aws"
  implementation "io.awspring.cloud:spring-cloud-starter-aws-messaging"

  ext.testContainersVersion = "1.17.6"
  testImplementation "org.springframework.cloud:spring-cloud-starter"
  testImplementation "org.springframework.cloud:spring-cloud-contract-wiremock"
  testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
  testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
}

checkstyle {
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.required = true
    xml.required = true
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
    vendor = JvmVendorSpec.ADOPTIUM
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    property "sonar.projectKey", "Health-Education-England_tis-trainee-ndw-exporter"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
